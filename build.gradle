// Build helpers
def snapshotBuild = project.version.endsWith("-SNAPSHOT")

// Build script
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5")
	}
}

// Projects
subprojects {

	apply plugin: "java"
	apply plugin: "jacoco"
	apply plugin: "org.sonarqube"
	apply plugin: "eclipse"
	
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	
	repositories {
		jcenter()
		if(snapshotBuild) {
			maven { url "https://oss.jfrog.org/libs-snapshot/" }
		}
	}

	sourceSets {
		main {
			java {
				srcDirs += "../filescanner-gtk-linux-x86_64/shared-src/main/java"
			}
		}
		test {
			java {
				srcDirs += "../filescanner-gtk-linux-x86_64/shared-src/test/java"
			}
		}
	}

	def buildPlatform = project.name.substring(rootProject.name.length())

	dependencies {
		compile("de.carne.common:java-default:3-SNAPSHOT")
		compile("de.carne.common:java-swt${buildPlatform}:2-SNAPSHOT")
		compile("de.carne.common:java-compression:3-SNAPSHOT")
		compile("org.apache.lucene:lucene-analyzers-common:7.0.0")
		compile("org.apache.lucene:lucene-queryparser:7.0.0") {
			exclude module: "lucene-sandbox"
		}
		testCompile("junit:junit:4.12")
	}

	jar {
		manifest {
			attributes(
				"Main-Class": "de.carne.Application"
			)
		}
		from configurations.runtime.collect {
			it.name.matches("(java-default-.*.jar)|(java-swt-.*.jar)") ? zipTree(it).matching { exclude "META-INF/**" } : it
		}
	}

	jacoco {
		toolVersion = "0.7.9"
	}
		
	jacocoTestReport {
		reports {
			xml.enabled false
			html.enabled true
			html.destination file("${buildDir}/reports/jacocoHtml")
			csv.enabled false
		}
	}

	eclipse {
		classpath {
			defaultOutputDir = file("eclipse-build")
		}
	}
}
